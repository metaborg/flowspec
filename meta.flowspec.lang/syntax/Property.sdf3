module Property

imports

  Common
  Pattern

context-free syntax // Properties

  Property.Def = [[PropName] [PropType] ([PropDir], [PropKind])]
  Property.DefForward = [[PropName] [PropType] ([PropKind])]
  Property.DefMay = [[PropName] [PropType] ([PropDir])]
  Property.DefForwardMay = [[PropName] [PropType]]
  
  PropType = PropTypeSimple
  PropType.Map = [[PropTypeDomain] -> [PropTypeRange]]
  
  PropTypeDomain.Name = [name]
  PropTypeDomain.Origin = [origin]
  PropTypeDomain.Sort = SortName
  
  PropTypeSimple.NameSet = [name]
  PropTypeSimple.OriginSet = [origin]
  PropTypeSimple.SortSet = SortName
  // TODO: add tuples
//  PropTypeSimple.Tuple = [[{PropTypeSimple "*"}+]]
//  PropTypeSimple = [([PropTypeSimple])] {bracket}

  PropTypeRange = PropTypeSimple
  PropTypeRange.NonSet = [[PropTypeDomain]?]
  
  PropDir.Forward  = [forward]
  PropDir.Backward = [backward]
  PropKind.Must = [strict]
  PropKind.May  = [loose]

context-free syntax // Property Rules

  PropRule.PropRule = [[{TLPattern ", "}+]: [{PropRuleBody "\n"}+]]
  PropRule.PropRuleWhere =
[[{TLPattern ", "}+]: [{PropRuleBody "\n"}+]
  where [{WhereClause "\n"}+]]
  
  PropRuleBody.Command = [[Command] [PropRef]]
//  PropRuleBody.CommandWhere = [[Command] [PropRef] where [{WhereClause "\n"}+]]

  Command.Adds = [adds]
  Command.Removes = [removes]
  Command.Overrides = [overrides]
  Command.Uses = [uses]
  
  PropRef.PropRef = [[PropName] [VarRef]]
  PropRef.MapPropRef = [[PropName] [VarRef] -> [VarRef]]
  
  VarRef.VarRef = ID {avoid}
  VarRef.This = [this]
  
  WhereClause.Contains = [[TLPattern] in [VarRef]]

template options

  keyword -/- [A-Za-z\-]
