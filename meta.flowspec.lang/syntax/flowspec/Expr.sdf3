module flowspec/Expr

imports

flowspec/Common
flowspec/Pattern

context-free syntax // expressions

Expr.Term = [[ConsName]([{Expr ","}*])]
Expr.QualRef = [[ModuleName].[Var]] {avoid}
Expr.Ref = Var
Expr.Prop = [[PropName]([Var])]
Expr.Tuple = [([Expr], [{Expr ", "}+])]
Expr = Literal
Expr.Start = [start]
Expr.End = [end]
// TODO: add ADT application

Expr = [([Expr])] {bracket}
Expr.Type = [[Var].type]
Expr.Appl = [[Expr] [Expr]] {left}
Expr.If = [if [Expr] then [Expr] else [Expr]]
Expr.Eq = [[Expr] == [Expr]] {left}
Expr.NEq = [[Expr] != [Expr]] {left}
Expr.Not = [![Expr]]
Expr.Match = [match [Expr] with [MatchArm+]] {longest-match}

Expr.SetLiteral = [{ [{Expr ","}+] }]
Expr.SetComp = [{ [Expr] | [{SetCompExpr ", "}+] }]
SetCompExpr.Source = [[Pattern] <- [Expr]]
SetCompExpr.Predicate = Expr

Expr.SetUnion = [[Expr] \/ [Expr]] {left}
Expr.SetDifference = [[Expr] \ [Expr]] {left}
Expr.SetContains = [[Expr] in [Expr]] {left}
Expr.SetIntersection = [[Expr] /\ [Expr]] {left}

MatchArm.MatchArm = [| [Pattern] => [Expr]]

Literal.Int = INT
Literal.String = STRING

context-free priorities
  Expr.Not > { left: Expr.Appl Expr.If Expr.Match } > { non-assoc: Expr.SetUnion Expr.SetIntersection Expr.SetContains Expr.SetDifference } > { left: Expr.Eq Expr.NEq }