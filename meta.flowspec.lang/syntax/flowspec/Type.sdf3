module flowspec/Type

imports

  flowspec/Common
  flowspec/Pattern
  flowspec/Expr

context-free syntax // Type definitions

  TypeDef.TypeDef = [[TypeName] [Var*] = [{ConsDef "\n|"}+]]
  ConsDef.Cons0 = [[ConsName]]
  ConsDef.Cons1 = [[ConsName] [TypeOrVar]]
  
  TypeOrVar = Type
  TypeOrVar.Var = Var

context-free syntax // Lattice definitions

  TypeDef.JSLImpl = [join semi-lattice of [TypeName] [Var*] {
    [LatticeImplDef*]
  }]
  TypeDef.MSLImpl = [meet semi-lattice of [TypeName] [Var*] {
    [LatticeImplDef*]
  }]
  TypeDef.CLImpl  = [complete lattice of [TypeName] [Var*] {
    [LatticeImplDef*]
  }]
  
  LatticeImplDef.TopDef = [top = [LatExpr]]
  LatticeImplDef.BottomDef = [bottom = [LatExpr]]
  LatticeImplDef.LUBDef = [lub [Var] [Var] = [LatExpr]]
  LatticeImplDef.GLBDef = [glb [Var] [Var] = [LatExpr]]

context-free syntax // Functions

  LatExpr = FunExpr
  LatExpr.Match = [match [Var] {
    [MatchArm+]
  }]
  MatchArm.MatchArm = [[ConsName] [FunPattern] => [LatExpr];]

context-free syntax // Type expressions

  Type = SimpleType
  SimpleType.Name = "name"
  SimpleType.Term = "term"
  SimpleType.UserType = [[TypeName]([Type*])] {avoid}
  SimpleType.Sort = SortName
  Type = [([Type])] {bracket}
  Type.Tuple = [[Type] * [Type]] {right}
  Type.Map = [map([Type], [Type])]
  Type.Set = [set([Type])]

context-free priorities
  { Type.Set } > Type.Tuple