module Flow

imports

  Common
  Property
  Pattern

context-free syntax

  FlowRule.MultiFlowRule = [[{TLPattern ", "}+]: [FlowRuleBody].] {prefer}
  FlowRule.EmptyRule = [[{TLPattern ", "}+].]
  // MultiFlowRules and EmptyRules get desugared to FlowRules later
  FlowRule.FlowRule = [[TLPattern]: [FlowRuleBody].]

  FlowRuleBody.Recurse = FlowRef
  FlowRuleBody.SingleEdge = [[FlowRef] -> [FlowRef]] {prefer}
  FlowRuleBody.Multiple = {FlowExpr ",\n"}+

  FlowRef = ID
  FlowRef = [entry] {reject}
  FlowRef = [exit] {reject}

  FlowExpr.Edge = [[FlowNode] -> [FlowNode]]
  FlowExpr.ConditionalEdge = [[FlowNode] -> [FlowNode] where [{FlowWhereClause ",\n"}+]]

  FlowNode.Ref = ID {avoid}
  FlowNode.Entry = [entry]
  FlowNode.Exit = [exit]

// This would probably be too powerful:
//  FlowWhereClause.CFGArrow = [[TLPattern] -> [TLPattern]]
  FlowWhereClause.HasProp = [[ID]: [PropName] [Pattern]]
  FlowWhereClause.HasMapProp = [[ID]: [PropName] [Pattern] -> [Pattern]]

template options

  keyword -/- [A-Za-z\-]