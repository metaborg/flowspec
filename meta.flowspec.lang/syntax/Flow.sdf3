module Flow

imports

  Common
  Property
  Pattern

context-free syntax

  FlowRule.FlowRule = [[TLPattern]: [FlowExpr]]
  FlowRule.FlowRuleWhere =
[[TLPattern]: [FlowExpr]
where [{ResolutionConstraint "\n"}+]]

  FlowRule.MultiRule = [[{NoNamePattern ", "}+]: [MultiRuleBody]] {prefer}
  MultiRuleBody.Opaques = "opaque"
  MultiRuleBody.Sequentials = "sequential"
  MultiRuleBody.Parallels = "parallel"

  FlowExpr.Ref = ID {avoid}
  FlowExpr.Jump = [jump [ID]]
  FlowExpr.Opaque = "opaque"
  FlowExpr.End = "end"
  
  FlowExpr.SeqList = [seq [ID]]
  FlowExpr.ParList = [par [ID]]

  FlowExpr = [([FlowExpr])] {bracket}
  FlowExpr.Sequential = [[FlowExpr], [FlowExpr]] {left}
  FlowExpr.Parallel = [[FlowExpr] || [FlowExpr]] {left}
  
  ResolutionConstraint.ResolvesTo = [[ID] |-> [ID]]

context-free priorities

  FlowExpr.Sequential > FlowExpr.Parallel

template options

  keyword -/- [A-Za-z\-]