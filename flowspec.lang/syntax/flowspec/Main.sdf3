module flowspec/Main

imports
  
flowspec/Common
flowspec/Flow
flowspec/Property
flowspec/Type
flowspec/Pattern
flowspec/Expr

context-free syntax
  
  Module.Module =
[module [ModuleName]

[{Import "\n"}*]

[{AstRoot "\n"}*]

[{Definition "\n"}*]
]

Import.Import = [imports [ModuleName]] {prefer}
Import.ExtImport = [imports external [StrategoModName]] {prefer}

// TODO: remove, already ignored
AstRoot.AstRoot = [ast root [ConsName]([{Wildcard ", "}*])]

Definition = LatticeDef
Definition = CFGRule
Definition = PropDef
Definition = PropRule
Definition = TypeDef
Definition = FunDef

FunDef.FunDef = [fun [Var]([{Pattern ", "}*]) = [Expr]]

context-free syntax // Alternative syntax

AltModule.Module =
[#alt-syntax
module [ModuleName]

[{Section "\n"}*]
]

Section.Imports =
[imports
  [{ModuleName "\n"}*]
  [{ImportExternalSection "\n"}*]]

ImportExternalSection.Externals =
[external
  [{StrategoModName "\n"}*]]

Section.CFGRules =
[control-flow rules
  [{AltCFGRule "\n"}*]]

Section.LatticeDefs =
[lattices
  [{AltLatticeDef "\n"}*]]

Section.PropDefs =
[properties
  [{AltPropDef "\n"}*]
  [{PropDefExternalSection "\n"}*]]

PropDefExternalSection.Externals =
[external
  [{AltPropDef "\n"}*]]

Section.PropRules =
[property rules
  [{PropRule "\n"}*]]

template options

keyword -/- [A-Za-z\-]