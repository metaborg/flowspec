module flowspec/editor-hover

imports
  nabl2/api
  nabl2shared
  nabl2runtime

  nabl2/runtime/pp
  nabl2/runtime/prelude/-
  nabl2/runtime/analysis/-
  flowspec/api
  
rules // Derived from https://github.com/MetaBorgCube/IceDust/blob/c21e7c6974565ae96148782622b169d7ddb2bc42/icedust/trans/editor/services.str

flowspec--editor-hover(language-pp): x@(target, position, ast, path, project-path) -> result
where <not(flowspec--hover-parent-for)> target
with analysis := <nabl2-get-resource-analysis> $[[project-path]/[path]]
with result := <flowspec--get-editor-properties(language-pp;flowspec--truncate-string(|200) | analysis)
                ;flowspec--properties-to-html> target

flowspec--get-editor-properties(language-pp | a) =
  flowspec--get-all-editor-properties(language-pp | a);filter(not(?(_,None())<+?(_,[])))

flowspec--get-all-editor-properties(language-pp | a): t ->
[ ("Target",               <language-pp            <+ !None()> t)
, ("Target Constructor",   <flowspec--pp-cons      <+ !None()> t)
, ("Type",                 <nabl2-get-ast-type(|a) <+ !None()> t)
, property-tuple*
]
with property-tuple* :=
  <try(flowspec--prop-names)
  ;mapconcat(\n ->
    [ (${Prop {n} (pre)},  <flowspec-get-property-pre(|a, n)  <+ !None()> t)
    , (${Prop {n} (post)}, <flowspec-get-property-post(|a, n) <+ !None()> t) ]\)> []

flowspec--pp-cons: t -> $[[<get-constructor> t]/[<get-arguments;length> t]] where not(is-string)

flowspec--pp-property: a -> a
where is-string
flowspec--pp-property: None() -> "-"
flowspec--pp-property: a -> <map(flowspec--pp-property; !("", <id>))> a
where is-list
flowspec--pp-property = topdown(try(rm-annotations)); write-to-string

flowspec--properties-to-html = map(flowspec--property-to-html); separate-by(|"<br/>\n"); concat-strings
flowspec--property-to-html: ("", prop) -> $[[<flowspec--property-to-html> prop]]
flowspec--property-to-html: (name, prop) -> $[<b>[name]</b>: [<flowspec--property-to-html> prop]]
flowspec--property-to-html: list -> $[<div>[<flowspec--properties-to-html> list]</div>]
where is-list
flowspec--property-to-html: prop -> <flowspec--escape-for-html> $[[<flowspec--pp-property> prop]]

flowspec--escape-for-html =
  string-replace(|"&", "&amp;")
; string-replace(|"<", "&lt;")
; string-replace(|">", "&gt;")
; string-replace(|"\n", "<br/>")

flowspec--truncate-string(|maxlen): s -> s'
with if <gt> (<strlen> s, maxlen) then
  trunc := <explode-string; split-at(|<dec; dec; dec> maxlen); ?(<id>,_); implode-string> s;
  s':= <conc-strings> (trunc, "...")
else
  s' := s
end

flowspec--hover-parent-for: a -> a // for constructors with no children hover parent, except for literals
where [] := <get-arguments> a
